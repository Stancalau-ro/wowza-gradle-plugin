apply plugin: 'groovy'
apply plugin: 'maven'
apply plugin: 'idea'


if (JavaVersion.current() != JavaVersion.VERSION_1_8) {
    throw new GradleException("Must Build with Java 1.8, but is ${JavaVersion.current()}")
}

group = 'ro.stancalau'
version = '1.1'
def artifact = 'wowza-gradle-plugin'

configurations {
    deployment
    published.extendsFrom archives
}

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    compile gradleApi()
    testCompile ([group: 'org.spockframework', name: 'spock-maven', version: '0.7-groovy-2.0'])
}

jar {
    baseName = artifact
}

task sourceJar(type: Jar) {
    description = 'An archive of the source code for Maven Central'
    baseName = artifact
    classifier = 'sources'
    from sourceSets.main.groovy
}

task groovydocJar(type: Jar, dependsOn: groovydoc) {
    description = 'An archive of the GroovyDocs for Maven Central'
    baseName = artifact
    classifier = 'javadoc'
    from fileTree(groovydoc.destinationDir)
}

artifacts {
    archives groovydocJar, sourceJar
}

install {
    repositories.mavenInstaller {
        pom.project(pomConfiguration)
    }
}

uploadPublished {
    repositories.mavenDeployer {
        repository( url: "https://api.bintray.com/content/stancalau/maven") {
            authentication(userName: 'stancalau', password: '')
        }
        pom.project(pomConfiguration)
    }
}

def getPomConfiguration() {
    return {
        name('Wowza Gradle Plugin')
        description('A Gradle plugin for building Wowza modules')
        url('https://bitbucket.org/stancalau/wowza-gradle-plugin')
        licenses {
            license {
                name 'GPL 3.0'
                distribution 'repo'
            }
        }
        developers {
            developer {
                id('stancalau')
                name('Stancalau Cristian')
            }
        }
        scm {
            connection('scm:https://stancalau@bitbucket.org/stancalau/wowza-gradle-plugin')
            developerConnection('scm:git@bitbucket.org:/stancalau/wowza-gradle-plugin.git')
            url('https://bitbucket.org/stancalau/wowza-gradle-plugin')
        }
    }
}
